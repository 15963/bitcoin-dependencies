# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>
# Modified by Ting-Wei Lan <lantw44@gmail.com> to work on MSYS2

_realname=miniupnpc
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.9.20141128
pkgrel=1
pkgdesc="A small UPnP client library/tool to access Internet Gateway Devices"
arch=('any')
url="http://miniupnp.free.fr"
license=('BSD')
depends=('sh')
source=("http://miniupnp.free.fr/files/$_realname-$pkgver.tar.gz")
sha256sums=('754419d9fec75deefa2c630a90eaf156bb008db5dbcdb662377715b7c1662e26')

build() {
  cp -r "$_realname-$pkgver" "$_realname-$pkgver-${MINGW_CHOST}"
  cd "$_realname-$pkgver-${MINGW_CHOST}"
  export CC="${MINGW_CHOST}-gcc"
  PATH="$PATH:`pwd`" make -f Makefile.mingw  CC="${CC}" \
    init wingenminiupnpcstrings upnpc-static
  make -f Makefile.mingw CC="${CC}"
}

package() {
  cd "$_realname-$pkgver-${MINGW_CHOST}"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/include/${_realname}"
  install -Dm644 \
    miniupnpc.h miniwget.h upnpcommands.h igd_desc_parse.h \
    upnpreplyparse.h upnperrors.h miniupnpctypes.h \
    portlistingparse.h declspec.h \
    "${pkgdir}${MINGW_PREFIX}/include/${_realname}"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/lib"
  install -Dm644 libminiupnpc.a "${pkgdir}${MINGW_PREFIX}/lib"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm644 miniupnpc.dll "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm644 man3/miniupnpc.3 \
    "${pkgdir}${MINGW_PREFIX}/share/man/man3/miniupnpc.3"
  install -Dm644 LICENSE \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
